' wyztrackerparser v0.1 20200515
' prepares a wyz tracker 0.5.2 .mus.asm file to be used from z88dk
' Copyleft 2020 by The Mojon Twins

#include "mtparser.bi"

Sub usage
	Print "$ wyztrackerparser.exe in.mus.asm out.h [cexport]"
	Print
	Print "in.mus.asm - WYZ Tracker 0.5.2 exported .mus.asm"
	Print "out.h - output filename"
	Print
	Print "This program:"
	Print " 1. Changes labels: to .labels"
	Print " 2. [cexport] Makes TABLA_PAUTAS & TABLA_SONIDOS exportable to C"
	Print " 3. Removes DATOS_NOTAS"
End Sub

Dim As Integer fIn, fOut, i
Dim As String linea, tokens (63)

If Command (2) = "" Then usage: End

fIn = FreeFile
Open Command (1) For Input As #fIn
fOut = FreeFile
Open Command (2) For Output As #fOut

Print #fOut, "// MTE MK1 (la Churrera) v5.0"
Print #fOut, "// Copyleft 2010-2014, 2020 by the Mojon Twins"
Print #fOut, ""
Print #fOut, "// Generated from " & Command (1)
Print #fOut, "// Generated by wyztrackerparser v0.1 20200515"
Print #fOut, ""
If Command (3) = "cexport" Then
	Print #fOut, "extern unsigned char tabla_pautas [0];"
	Print #fOut, "extern unsigned char tabla_sonidos [0];"
	Print #fOut, ""
End If

Print #fOut, "#asm"

While Not Eof (fIn)
	Line Input #fIn, linea
	parseTokenizeString linea, tokens (), "" & Chr(9), ";"

	If tokens (0) = "DATOS_NOTAS:" Then Exit While

	' Identify first word:
	Select Case tokens (0)
		Case "TABLA_PAUTAS:":
			If Command (3) = "cexport" Then
				tokens (0) = "._tabla_pautas"
			Else
				tokens (0) = ".TABLA_PAUTAS"
			End If
		Case "TABLA_SONIDOS:":
			If Command (3) = "cexport" Then
				tokens (0) = "._tabla_sonidos"
			Else
				tokens (0) = ".TABLA_SONIDOS"
			End If
		Case Else
			If tokens (0) <> "" Then 
				If Right (tokens (0), 1) = ":" Then tokens (0) = "." & left (tokens (0), len (tokens (0)) - 1)
			End If
	End Select

	' Build output
	If tokens (0) <> "" Then 
		Print #fOut, "    "; tokens (0)
		Print #fOut, "        ";
		For i = 1 To uBound (tokens)
			If tokens (i) = "" Then Exit For
			If lCase (tokens (i)) = "db" Then tokens (i) = "defb"
			If lCase (tokens (i)) = "dw" Then tokens (i) = "defw"
			Print #fOut, tokens (i); " ";
		Next i
		Print #fOut, ""
	Else 
		Print #fOut, "    " & linea
	End If
Wend

Print #fOut, "#endasm"

Close
